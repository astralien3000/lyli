file : stmt_list

expr : atomic
     | list

stmt : expr (expr)*

atomic : string
       | longstring
       | char
       | float
       | integer
       | symbol

list: "(" stmt_list ")"
    | "{" stmt_list "}"
    | "[" stmt_list "]"

stmt_list: (stmt ",")* stmt?
         | (stmt ";")* stmt?

string : STRING

longstring : LONGSTRING

char : CHAR_LITERAL

integer : INT_LITERAL
        | BIN_LITERAL
        | OCT_LITERAL
        | DEC_LITERAL
        | HEX_LITERAL
        | ZERO_LITERAL

float : FLOAT_LITERAL

symbol : IDENTIFIER | OPERATOR

INT_LITERAL : /[1-9]([0-9]|_[0-9])*/
BIN_LITERAL : /0b[01]([01]|_[01])*/
OCT_LITERAL : /0o[0-7]([0-7]|_[0-7])*/
DEC_LITERAL : /0d[0-9]([0-9]|_[0-9])*/
HEX_LITERAL : /0x[0-9a-fA-F]([0-9a-fA-F]|_[0-9a-fA-F])*/
ZERO_LITERAL : /0/

FLOAT_LITERAL : FLOAT_INTEGER FLOAT_DECIMAL FLOAT_EXPONENT
              | FLOAT_INTEGER FLOAT_DECIMAL
              | FLOAT_INTEGER FLOAT_EXPONENT

FLOAT_INTEGER : /(0|[1-9]([0-9]|_[0-9])*)/
FLOAT_DECIMAL : /\.[0-9]([0-9]|_[0-9])*/
FLOAT_EXPONENT : /[eE]-?[0-9]([0-9]|_[0-9])*/

STRING : /\"[^\"\n]*\"/
LONGSTRING : /\"\"\"([^\"]|\"\"?[^\"])+\"\"\"/
CHAR_LITERAL : /'[^'\n]'/

IDENTIFIER : /[^\W\d]\w*/

OPERATOR : /[.*\/+\-:&|=!?~#^@$%<>]+/

WS : /\s+/
%ignore WS
